plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.9.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven{
		name = "Blamejared"
		url ="https://maven.blamejared.com/"
	}
	maven{
		name = "Architectury"
		url ="https://maven.architectury.dev/"
	}
	maven{
		name = "JamesWhiteShirt"
		url ="https://maven.jamieswhiteshirt.com/libs-release/"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
	}
	maven {
		name = "Terraformermc"
		url = "https://maven.terraformersmc.com/releases"
	}
	maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
	maven {
		name = "Jitpack"
		url = 'https://jitpack.io'
	}
	maven{
		name = "Devos"
		url = "https://mvn.devos.one/snapshots/"
	}
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
	maven { url "https://maven.shedaniel.me" }
	mavenCentral()
}

loom {
    splitEnvironmentSourceSets()

	mods {
		"hexlink" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation("net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}")

	modImplementation "at.petra-k.paucal:paucal-fabric-$minecraft_version:$paucalVersion"
	modImplementation "vazkii.patchouli:Patchouli:$minecraft_version-$patchouliVersion-FABRIC"
	modImplementation("at.petra-k.hexcasting:hexcasting-fabric-$minecraft_version:$hexcastingVersion"){
		exclude module: "phosphor"
	}

	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:9.1.632"
	modImplementation "curse.maven:emi-loot-681783:4105999"
	
	// Uncomment the following line to enable the deprecated Fabric API modules. 
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 17
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}